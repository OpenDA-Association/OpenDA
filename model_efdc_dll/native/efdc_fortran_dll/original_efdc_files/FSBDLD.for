      FUNCTION FSBDLD(DIASED,GPDIASED,D50,DEP,PEXP,PHID,CSHIELDS,  
     &    SBDLDP,ISOPT)  
C  
C CHANGE RECORD  
C  MODIFED TO INCLUDE ALTERNATE FORMULAS FOR BED LOAD PHI  
C  
C  
C **  CALCULATES DIMNSIONLESS BED LOAD TRANSPORT COEFFICIENT  
C **  ISOPT=0  USE CONSTANT VALUE  
C  
      IF(ISOPT.EQ.0) FSBDLD=SBDLDP  
C  
C **  ISOPT=1  BASED ON  
C **  
C **  VAN RIJN, L. C., 1984: SEDIMENT TRANSPORT, PART I: BED  
C **  LOAD TRANSPORT, J. HYDRAULIC ENGINEERING, 110, 1431-1455.  
C  
      IF(ISOPT.EQ.1)THEN  
        RD=DIASED*SQRT(GPDIASED)*1.E6  
        RD=(1./RD)**0.2  
        TMP=CSHIELDS**2.1  
        FSBDLD=0.053*RD/TMP  
      ENDIF  
C  
C **  ISOPT=2  BASED ON MODIFIED ENGULAND-HANSEN FORMULA  
C **  REFERENCE TO BE ADDED  
C  
      IF(ISOPT.EQ.2)THEN  
        TMP1=(DEP/D50)**0.33333  
        TMP2=(PEXP/PHID)**1.125  
        FSBDLD=2.0367*TMP1*TMP2  
      ENDIF  
C  
C **  ISOPT=3  BASED ON WU, WANG AND JIA, J. HYDR. RES. V38, 2000  
C  
      IF(ISOPT.EQ.3)THEN  
        TMP1=0.03*((PHID/PEXP)**0.6)  
        TMP2=TMP1**2.2  
        FSBDLD=0.0053/TMP2  
      ENDIF  
      RETURN  
      END  

